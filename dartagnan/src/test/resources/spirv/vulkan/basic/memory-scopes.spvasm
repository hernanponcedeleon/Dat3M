; @Output: forall (%out_th[0]==1 and %out_th[1]==1 and %out_th[2]==1 and %out_th[3]==1)
; @Output: forall (%out_wg[0]==2 and %out_wg[1]==2 and %out_wg[2]==2 and %out_wg[3]==2)
; @Output: forall (%var_dv==4)
; @Config: 2, 1, 2
; SPIR-V
; Version: 1.0
; Schema: 0
               OpCapability Shader
               OpCapability VulkanMemoryModel
               OpMemoryModel Logical Vulkan
               OpEntryPoint GLCompute %main "main" %ids
               OpSource GLSL 450
               OpDecorate %ids BuiltIn GlobalInvocationId

       %void = OpTypeVoid
       %bool = OpTypeBool
       %func = OpTypeFunction %void
       %uint = OpTypeInt 32 0
     %v3uint = OpTypeVector %uint 3
     %v4uint = OpTypeVector %uint 4

     %ptr_in = OpTypePointer Input %uint
    %ptr_out = OpTypePointer Output %uint
  %ptr_v3_in = OpTypePointer Input %v3uint
 %ptr_v4_out = OpTypePointer Output %v4uint
%ptr_uint_th = OpTypePointer Private %uint
%ptr_uint_wg = OpTypePointer Workgroup %uint
%ptr_uint_dv = OpTypePointer Uniform %uint

         %c0 = OpConstant %uint 0
         %c1 = OpConstant %uint 1
         %c2 = OpConstant %uint 2
         %c4 = OpConstant %uint 4
       %c322 = OpConstant %uint 322
       %c328 = OpConstant %uint 328

     %var_th = OpVariable %ptr_uint_th Private %c0
     %var_wg = OpVariable %ptr_uint_wg Workgroup %c0
     %var_dv = OpVariable %ptr_uint_dv Uniform %c0
     %out_th = OpVariable %ptr_v4_out Output
     %out_wg = OpVariable %ptr_v4_out Output
        %ids = OpVariable %ptr_v3_in Input

       %main = OpFunction %void None %func
    %label_1 = OpLabel

     %id_ptr = OpAccessChain %ptr_in %ids %c0
         %id = OpLoad %uint %id_ptr

%var_th_orig = OpAtomicIIncrement %uint %var_th %c4 %c328
%var_wg_orig = OpAtomicIIncrement %uint %var_wg %c2 %c328
%var_dv_orig = OpAtomicIIncrement %uint %var_dv %c1 %c328

               OpControlBarrier %c2 %c1 %c328

         %th = OpAtomicLoad %uint %var_th %c4 %c322
         %wg = OpAtomicLoad %uint %var_wg %c2 %c322
         %dv = OpAtomicLoad %uint %var_dv %c1 %c322

 %out_th_ptr = OpAccessChain %ptr_out %out_th %id
 %out_wg_ptr = OpAccessChain %ptr_out %out_wg %id

               OpStore %out_th_ptr %th
               OpStore %out_wg_ptr %wg

               OpReturn
               OpFunctionEnd
