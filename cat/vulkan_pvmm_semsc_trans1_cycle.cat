VULKAN
(* Vulkan Memory Model *)
(* With availability-visibility chains supported *)

// Base relations:
// vloc: events accessing the same virtual location, aka "same reference (sref)" in the Vulkan spec
// syncbar: synchronize by barriers with same logical barrier resource (barID)
// ssg: same subgroup
// swg: same workgroup
// sqf: same queuefamily
// ssw: system-synchronizes-with

// Tags:
// SC0, SC1, SC2, SC3: storage class 0, 1, 2, 3
// SEMSC0, SEMSC1, SEMSC2, SEMSC3: storage class semantics 0, 1, 2, 3
// SG, WG, QF, DV: subgroup, workgroup, queuefamily, device domains
// AV, VIS: availability, visibility
// AVDEVICE, VISDEVICE: availability, visibility op to the device domain

(*******************)
(*  Auxiliaries *)
(*******************)

(* Tags *)
let PRIV = (R | W) \ NONPRIV // PRIV is the complement of NONPRIV

let SC01 = SC0 | SC1
let SC02 = SC0 | SC2
let SC03 = SC0 | SC3
let SC12 = SC1 | SC2
let SC13 = SC1 | SC3
let SC23 = SC2 | SC3
let SC012 = SC01 | SC2
let SC013 = SC01 | SC3
let SC023 = SC02 | SC3
let SC123 = SC12 | SC3
let SC0123 = SC012 | SC3

let SEMSC01 = SEMSC0 & SEMSC1
let SEMSC02 = SEMSC0 & SEMSC2
let SEMSC03 = SEMSC0 & SEMSC3
let SEMSC12 = SEMSC1 & SEMSC2
let SEMSC13 = SEMSC1 & SEMSC3
let SEMSC23 = SEMSC2 & SEMSC3
let SEMSC012 = SEMSC01 & SEMSC2
let SEMSC013 = SEMSC01 & SEMSC3
let SEMSC023 = SEMSC02 & SEMSC3
let SEMSC123 = SEMSC12 & SEMSC3
let SEMSC0123 = SEMSC012 & SEMSC3

let AVSHADER = (AV | SEMAV) & DV
let VISSHADER = (VIS | SEMVIS) & DV

let AVQF = (AV | SEMAV) & (DV | QF)
let VISQF = (VIS | SEMVIS) & (DV | QF)

let AVWG = (AV | SEMAV) & (DV | QF | WG)
let VISWG = (VIS | SEMVIS) & (DV | QF | WG)

let AVSG = AV | SEMAV
let VISSG = VIS | SEMVIS

(* Relations *)
let inscope = (DV * DV) |
              (sqf & ((DV | QF) * (DV | QF))) |
              (swg & ((DV | QF | WG) * (DV | QF | WG))) |
              (ssg & ((DV | QF | WG | SG) * (DV | QF | WG | SG)))

// mutually ordered atomics
let mutordatom = (loc & vloc & inscope & (ATOM * ATOM)) \ id

let avvisSem = (SC0123 * (AVDEVICE | VISDEVICE))
                | (SC0 * ((SEMAV | SEMVIS) & SEMSC0))
                | (SC1 * ((SEMAV | SEMVIS) & SEMSC1))
                | (SC2 * ((SEMAV | SEMVIS) & SEMSC2))
                | (SC3 * ((SEMAV | SEMVIS) & SEMSC3))
let avvisSet = [AV | VIS]; (vloc & loc)
let avvisinc = ((avvisSem | avvisSem^-1) \ id) |
               (avvisSet | avvisSet^-1)

let posctosem = ((SC0 * SEMSC0) | (SC1 * SEMSC1) | (SC2 * SEMSC2) | (SC3 * SEMSC3)) & po
let posemtosc = ((SEMSC0 * SC0) | (SEMSC1 * SC1) | (SEMSC2 * SC2) | (SEMSC3 * SC3)) & po

let asmo = co & ((ATOM | IW) * ATOM) & mutordatom

let imm(r) = r \ (r; r+)
let rs = [REL & ATOM]; (imm(asmo); [RMW])*
let hypors = [W & ATOM]; (imm(asmo); [RMW])*

// synchronizes-with is similar to C++, with an additional case for fence->cbar->cbar->fence
let sw = inscope & (
    ([REL & ATOM]; rs; (rf & mutordatom); [ACQ & ATOM]) |                                               // atomic->atomic
    ([REL & F]; posemtosc; [ATOM & W]; hypors; (rf & mutordatom); [ACQ & ATOM]) |                       // fence->atomic
    ([REL & ATOM]; rs; (rf & mutordatom); [ATOM & R]; posctosem; [ACQ & F]) |                           // atomic->fence
    ([REL & F]; posemtosc; [ATOM & W]; hypors; (rf & mutordatom); [ATOM & R]; posctosem; [ACQ & F]) |   // fence->fence
    // (stor[CBAR]) terms are redundant because scbarinst is an equivalence relation on scbarinst, but they make the sequence of instructions more clear.
    ([REL & F]; po?; [CBAR]; ((syncbar & inscope) \ id); [CBAR]; po?; [ACQ & F])                        // fence->cbar->cbar->fence,
)

let ithbsemsc(SC, SEMSC) = (ssw |
        ([SEMSC]; sw; [SEMSC]) |
        ([F & SEMSC]; po ; [F & SEMSC]) |
        ([SC]; po; [REL & SEMSC]) |
        ([ACQ & SEMSC]; po; [SC])
    )+

let ithbsemsc0_b = ithbsemsc(SC0, SEMSC0)
let ithbsemsc1_b = ithbsemsc(SC1, SEMSC1)
let ithbsemsc2_b = ithbsemsc(SC2, SEMSC2)
let ithbsemsc3_b = ithbsemsc(SC3, SEMSC3)

let ithbsemsc01_b = ithbsemsc(SC01, SEMSC01)
let ithbsemsc02_b = ithbsemsc(SC02, SEMSC02)
let ithbsemsc03_b = ithbsemsc(SC03, SEMSC03)
let ithbsemsc12_b = ithbsemsc(SC12, SEMSC12)
let ithbsemsc13_b = ithbsemsc(SC13, SEMSC13)
let ithbsemsc23_b = ithbsemsc(SC23, SEMSC23)

let ithbsemsc012_b = ithbsemsc(SC012, SEMSC012)
let ithbsemsc013_b = ithbsemsc(SC013, SEMSC013)
let ithbsemsc023_b = ithbsemsc(SC023, SEMSC023)
let ithbsemsc123_b = ithbsemsc(SC123, SEMSC123)

let ithbsemsc0123 = ithbsemsc(SC0123, SEMSC0123)

let ithbsemsc012 = ithbsemsc012_b ; (ithbsemsc0123 ; ithbsemsc012_b)*
let ithbsemsc013 = ithbsemsc013_b ; (ithbsemsc0123 ; ithbsemsc013_b)*
let ithbsemsc023 = ithbsemsc023_b ; (ithbsemsc0123 ; ithbsemsc023_b)*
let ithbsemsc123 = ithbsemsc123_b ; (ithbsemsc0123 ; ithbsemsc123_b)*

let ithbsemsc01 = ithbsemsc01_b ; ((ithbsemsc012 | ithbsemsc013) ; ithbsemsc01_b)*
let ithbsemsc02 = ithbsemsc02_b ; ((ithbsemsc012 | ithbsemsc023) ; ithbsemsc02_b)*
let ithbsemsc03 = ithbsemsc03_b ; ((ithbsemsc013 | ithbsemsc023) ; ithbsemsc03_b)*
let ithbsemsc12 = ithbsemsc12_b ; ((ithbsemsc012 | ithbsemsc123) ; ithbsemsc12_b)*
let ithbsemsc13 = ithbsemsc13_b ; ((ithbsemsc013 | ithbsemsc123) ; ithbsemsc13_b)*
let ithbsemsc23 = ithbsemsc23_b ; ((ithbsemsc023 | ithbsemsc123) ; ithbsemsc23_b)*

let ithbsemsc0 = ithbsemsc0_b ; ((ithbsemsc01 | ithbsemsc02 | ithbsemsc03) ; ithbsemsc0_b)*
let ithbsemsc1 = ithbsemsc1_b ; ((ithbsemsc01 | ithbsemsc12 | ithbsemsc13) ; ithbsemsc1_b)*
let ithbsemsc2 = ithbsemsc2_b ; ((ithbsemsc02 | ithbsemsc12 | ithbsemsc23) ; ithbsemsc2_b)*
let ithbsemsc3 = ithbsemsc3_b ; ((ithbsemsc03 | ithbsemsc13 | ithbsemsc23) ; ithbsemsc3_b)*

// happens-before
let hb = (po |
        ithbsemsc0 | ithbsemsc1 | ithbsemsc2 | ithbsemsc3 |
        ithbsemsc01 | ithbsemsc02 | ithbsemsc03 | ithbsemsc12 | ithbsemsc13 | ithbsemsc23 |
        ithbsemsc012 | ithbsemsc013 | ithbsemsc023 | ithbsemsc123 |
        ithbsemsc0123
)

// chains will produce the availability/visibility chains with this pattern:
let E = (M | F | CBAR | AVDEVICE | VISDEVICE) \ IW
let chains = E * E

let avsg = [AVSG]
let avwg = (chains & (avsg; (hb & ssg & avvisinc))?); [AVWG]
let avqf = (chains & ((avsg; (hb & ssg & avvisinc))? ; (avwg; (hb & swg & avvisinc))?)); [AVQF]
let avsh = (chains & ((avsg; (hb & ssg & avvisinc))? ; (avwg; (hb & swg & avvisinc))?; (avqf; (hb & sqf & avvisinc))?)); [AVSHADER]
let avdv = [AVDEVICE]

let vissg = [VISSG]
let viswg = [VISWG]; (chains & (((hb & ssg & avvisinc); vissg)?))
let visqf = [VISQF]; (chains & (((hb & swg & avvisinc) ; viswg)?; ((hb & ssg & avvisinc); vissg)?))
let vissh = [VISSHADER]; (chains & (((hb & sqf & avvisinc) ; visqf)?; ((hb & swg & avvisinc); viswg)?; ((hb & ssg & avvisinc); vissg)?))
let visdv = [VISDEVICE]

// loc order, relates memory accesses to the same location
let locord = loc &
             (  (hb & int & vloc) |
                ([R \ PRIV]; hb; [(R | W) \ PRIV]) | // RaR, WaR (non-private)
                ([R]; ssw+; [R | W]) | // RaR, WaR (any)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avsg; (hb & ssg);                          [W \ PRIV])) |   // WaW (via subgroup instance domain)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avsg; (hb & ssg); vissg; (po? & avvisinc); [R \ PRIV])) |   // RaW (via subgroup instance domain)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avwg; (hb & swg);                          [W \ PRIV])) |   // WaW (via subgroup instance domain)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avwg; (hb & swg); viswg; (po? & avvisinc); [R \ PRIV])) |   // RaW (via workgroup instance domain)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avqf; (hb & sqf);                          [W \ PRIV])) |   // WaW (via queue family instance domain)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avqf; (hb & sqf); visqf; (po? & avvisinc); [R \ PRIV])) |   // RaW (via queue family instance domain)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avsh; (hb);                                [W \ PRIV])) |   // WaW (via shader domain)
                (vloc & ([W \ PRIV]; (po? & avvisinc); avsh; (hb);       vissh; (po? & avvisinc); [R \ PRIV])) |   // RaW (via shader domain)
                (       ([W];        (hb & avvisinc);  avdv; (hb);                                [W]))        |   // WaW (via device domain)
                (       ([W];        (hb & avvisinc);  avdv; (hb);       visdv; (hb & avvisinc);  [R]))            // RaW (via device domain)
             )

// From-read is defined based on scoped modification order
// instead of coherence. For reads that read-from init, they
// from-read all writes at the same location.

// The definition of the original Vulkan model is too restrictive and forbids CoWW-RR
// let fr = (rf^-1 ; (W * W) & locord) | (rf^-1; asmo) | (([IW]; rf)^-1; ((loc;[W]) \ id))
let fr = ((rf & locord)^-1 ; (W * W) & locord) | (rf^-1; asmo) | (([IW]; rf)^-1; ((loc;[W]) \ id))
let fre = fr & ext

(***********************)
(* Memory Model Axioms *)
(***********************)

// locord, rf, fr, asmo must not form cycles
// acyclic (locord | rf | fr | asmo) as consistency-cycle
let cycle = (locord | rf | fr | asmo)+
flag ~empty cycle

// non-atomic cannot read-from a value that is shadowed by another write
empty (rf; ([R \ ATOM])) & (([W]; locord); ([W]; locord)+) as consistency-rf

// rmw events are atomic
empty rmw & (fre; (asmo & ext)) as atomic

(***************)
(* Data Races *)
(***************)

let pair_with_write = ((W * W) | (W * R) | (R * W)) \ ((IW * _) | (_ * IW)) // ignore IW
let dr = loc & (pair_with_write \ mutordatom \ id \ (locord | locord^-1))
flag ~empty dr as racy