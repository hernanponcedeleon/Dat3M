OpenCL
(* OpenCL Memory Model *)

(*
* This model is based on:
* https://multicore.doc.ic.ac.uk/overhauling/opencl_scopedsc.cat
*)

include "opencl-overhauling-basic.cat"

(****************************************)
(* Sequential consistency, simplified,  *)
(* with scoped SC axioms                *)
(****************************************)

let scp = Fsb?; (rb | mo | (ghb | lhb)); sbF?
acyclic (SC*SC) & scp & incl as O-Sscoped

(***************)
(*    Races    *)
(***************)

let cnf = ((W * W) | (W * R) | (R * W)) & loc

let dr = cnf & ~(ghb | lhb) & ~(ghb | lhb)^-1 & ~wi & ~incl
flag ~empty dr as data_race

(* Barrier divergence *)
let bsw = bar_sw(GLOBAL) | bar_sw(LOCAL)
let bd = [EF] & ((~wi & swg); unv) \ (bsw ; unv)
flag ~empty bd as barrier_divergence