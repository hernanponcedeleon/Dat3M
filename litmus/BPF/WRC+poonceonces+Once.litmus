BPF WRC+poonceonces+Once

(*
 * Result: Sometimes
 *
 * This litmus test is an extension of the message-passing pattern,
 * where the first write is moved to a separate process.  Note that this
 * test has no ordering at all.
 *)

{
0:r0=x;

1:r0=x;
1:r1=y;

2:r0=x;
2:r1=y;
}

P0                     | P1                     | P2                     ;
*(u32 *)(r0 + 0) = 1   | r2 =  *(u32 *)(r0 + 0) | r2 = *(u32 *)(r1 + 0)  ;
                       | *(u32 *)(r1 + 0) = 1   | r3 = *(u32 *)(r0 + 0)  ;

exists (1:r2=1 /\ 2:r2=1 /\ 2:r3=0)

(*
 * The write to x in P0 is visible to P1 but not to P2, this means the LKMM
 * is not other multicopy atomic.
 *
 * Other multicopy atomicity: A write from a processor should be visible to
 * all other processors at the same time. It can be visible earlier to the
 * processor doing the write.
 *)
