LISA TC17
(*
 * Result: Never
 *
 * http://www.cs.umd.edu/~pugh/java/memoryModel/unifiedProposal/testcases.html
 *
 * Decision: Allowed.  A compiler could determine that at r1 = x in
 *	thread 1, is must be legal for to read x and see the value
 *	42. Changing r1 = x to r1 = 42 would allow y = r1 to be
 *	transformed to y = 42 and performed earlier, resulting in the
 *	behavior in question.
 *
 * Note: Maybe for Java, but relaxed atomics in C11 prohibit violating
 *	cache coherence.  Adjusted result accordingly.
 *)
{
x = 0;
y = 0;
}
 P0                | P1           ;
 r[once] r3 x      | r[once] r2 y ;
 mov r9 (eq r3 42) | w[once] x r2 ;
 b[] r9 SKIP0      |              ;
 w[once] x 42      |              ;
 SKIP0:            |              ;
 r[once] r1 x      |              ;
 w[once] y r1      |              ;
locations [0:r3]
exists (0:r1=1:r2 /\ ~1:r2=0 /\ ~1:r2=42)
(* exists (0:r1=42 /\ 0:r3=42 /\ 1:r2=42) Note: fails to note S results. *)

