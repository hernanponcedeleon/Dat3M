LISA LISA2qR1G
(*
 * Result: Never
 *
 * A single RCU grace period with two RCU read-side critical sections,
 * however, with a release-acquire relationship between the first
 * critical section and the beginning of the grace period.  This
 * release-acquire relationship forces the first critical section to
 * entirely precede the grace period, so that there can be no overlap
 * with P2's RCU read-side critical section.  Cycle prohibited.
 *)
{
x0 = 0;
x1 = 0;
x2 = 0;
}
 P0                 | P1               | P2                 ;
 f[rcu_read_lock]   | r[acquire] r1 x1 | f[rcu_read_lock]   ;
 r[once] r2 x0      | f[sync]          | r[once] r2 x2      ;
 w[release] x1 1    | w[once] x2 1     | w[once] x0 1       ;
 f[rcu_read_unlock] |                  | f[rcu_read_unlock] ;
exists (1:r1=1 /\ 2:r2=1 /\ 0:r2=1)
