LISA LISAq3R1G
(*
 * Result: Never
 *
 * One RCU grace period and three RCU read-side critical sections.
 * This would normally allow cycles, but the release-acquire relationship
 * between P3 and P0 forces the entirety of P3 to precede the grace
 * period.  Cycle prohibited.
 *)
{
x0 = 0;
x1 = 0;
x2 = 0;
x3 = 0;
}
 P0                 | P1            | P2                 | P3                 ;
 f[rcu_read_lock]   | r[once] r1 x1 | f[rcu_read_lock]   | f[rcu_read_lock]   ;
 r[acquire] r2 x0   | f[sync]       | r[once] r2 x2      | r[once] r3 x3      ;
 w[once] x1 1       | w[once] x2 1  | w[once] x3 1       | w[release] x0 1    ;
 f[rcu_read_unlock] |               | f[rcu_read_unlock] | f[rcu_read_unlock] ;
exists (1:r1=1 /\ 2:r2=1 /\ 3:r3=1 /\ 0:r2=1)
